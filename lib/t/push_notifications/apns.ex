defmodule T.PushNotifications.APNS do
  @moduledoc false

  alias T.Accounts.APNSDevice
  import T.Gettext
  require Logger

  @adapter Application.compile_env!(:t, [__MODULE__, :adapter])

  @spec push(APNS.notification()) :: APNS.response()
  def push(%{device_id: device_id} = notification) do
    with {:error, reason} = error when reason in [:bad_device_token, :unregistered] <-
           @adapter.push(notification) do
      Logger.warn("received error=#{reason} for #{device_id}")
      error
    end
  end

  @spec default_topic :: String.t()
  def default_topic do
    Application.fetch_env!(:t, __MODULE__)
    |> Keyword.fetch!(:default_topic)
  end

  @spec apns_env(%APNSDevice{} | String.t() | nil) :: APNS.env()
  def apns_env(%APNSDevice{env: env}), do: apns_env(env)
  def apns_env("prod"), do: :prod
  def apns_env("sandbox"), do: :dev
  def apns_env(nil), do: :dev

  @spec base_alert_payload(String.t(), map, map) :: map
  defp base_alert_payload(type, alert, extra) do
    Map.merge(extra, %{
      "type" => type,
      "aps" => %{"alert" => alert, "badge" => 1}
    })
  end

  @spec base_alert_payload(String.t(), map) :: map
  defp base_alert_payload(type, alert) do
    %{
      "type" => type,
      "aps" => %{"alert" => alert, "badge" => 1}
    }
  end

  @spec build_alert_payload(String.t(), map) :: map
  def build_alert_payload(template, data)

  # TODO remove
  def build_alert_payload("match" = type, data) do
    alert = %{
      "title" => dgettext("apns", "Ð­Ñ‚Ð¾ Ð½Ð¾Ð²Ñ‹Ð¹ Ð¼ÑÑ‚Ñ‡!"),
      "body" => dgettext("apns", "Ð¡ÐºÐ¾Ñ€ÐµÐµ Ð·Ð°Ñ…Ð¾Ð´Ð¸!")
    }

    base_alert_payload(type, alert, data)
  end

  # TODO remove
  def build_alert_payload(
        "match_about_to_expire" = type,
        %{"name" => name, "gender" => gender} = data
      ) do
    alert = %{
      "title" => dgettext("apns", "Match with %{name} is about to expire", name: name),
      "body" =>
        dgettext("apns", "Last chance to send %{pronoun_to} a message âœ¨",
          pronoun_to: pronoun_to(gender)
        )
    }

    base_alert_payload(type, alert, data)
  end

  # TODO remove
  def build_alert_payload(
        "match_about_to_expire_please_reply" = type,
        %{"name" => name, "gender" => gender} = data
      ) do
    alert = %{
      "title" => dgettext("apns", "Match with %{name} is about to expire", name: name),
      "body" =>
        dgettext("apns", "Last chance to reply to %{pronoun_belonging_to} message âœ¨",
          pronoun_belonging_to: pronoun_belonging_to(gender)
        )
    }

    base_alert_payload(type, alert, data)
  end

  def build_alert_payload(type, %{"name_from" => name_from, "gender_from" => gender_from} = data)
      when type in [
             "invitation",
             "acceptance",
             "text",
             "message",
             "drawing",
             "video",
             "audio",
             "spotify",
             "contact",
             "photo"
           ] do
    verb_ending_ru =
      case gender_from do
        "F" -> "Ð°"
        "N" -> "Ð¸"
        "M" -> ""
      end

    body =
      case type do
        "invitation" ->
          dgettext("apns", "invited you to connect")

        "acceptance" ->
          dgettext("apns", "accepted%{verb_ending_ru} your invitation",
            verb_ending_ru: verb_ending_ru
          )

        "text" ->
          data["data"]["value"]

        "message" ->
          dgettext("apns", "sent%{verb_ending_ru} a message", verb_ending_ru: verb_ending_ru)

        "drawing" ->
          dgettext("apns", "sent%{verb_ending_ru} a drawing", verb_ending_ru: verb_ending_ru)

        "video" ->
          dgettext("apns", "sent%{verb_ending_ru} a video message", verb_ending_ru: verb_ending_ru)

        "audio" ->
          dgettext("apns", "sent%{verb_ending_ru} a voice message", verb_ending_ru: verb_ending_ru)

        "spotify" ->
          dgettext("apns", "sent%{verb_ending_ru} a music track", verb_ending_ru: verb_ending_ru)

        "contact" ->
          dgettext("apns", "sent%{verb_ending_ru} a contact", verb_ending_ru: verb_ending_ru)

        "photo" ->
          dgettext("apns", "sent%{verb_ending_ru} a photo", verb_ending_ru: verb_ending_ru)
      end

    alert = %{"title" => name_from, "body" => body}
    base_alert_payload(type, alert, data)
  end

  def build_alert_payload(
        "private_page_available" = type,
        %{"name_of" => name_of, "gender_of" => gender_of} = data
      ) do
    verb_ending_ru =
      case gender_of do
        "F" -> "Ð°"
        "N" -> "Ð¸"
        "M" -> ""
      end

    title =
      dgettext("apns", "%{name_of} replied%{verb_ending_ru} to you",
        name_of: name_of,
        verb_ending_ru: verb_ending_ru
      )

    body =
      dgettext("apns", "Now you can see %{pronoun_belonging_to} private pages",
        pronoun_belonging_to: pronoun_belonging_to(gender_of)
      )

    alert = %{"title" => title, "body" => body}
    base_alert_payload(type, alert, data)
  end

  # TODO remove
  def build_alert_payload("invite" = type, data) do
    %{"user_id" => user_id, "name" => name} = data
    alert = %{"title" => dgettext("apns", "%{name} invited you to connect", name: name)}
    base_alert_payload(type, alert, %{"user_id" => user_id})
  end

  def build_alert_payload("complete_onboarding" = type, _data) do
    alert = %{
      "title" => dgettext("apns", "Hey, create your own cool profile âœ¨"),
      "body" => dgettext("apns", "Meet interesting people âœŒï¸")
    }

    base_alert_payload(type, alert)
  end

  def build_alert_payload("upgrade_app" = type, _data) do
    alert = %{
      "title" => dgettext("apns", "Update the app in the App Store âœ¨"),
      "body" => dgettext("apns", "The current version is no longer supported ðŸ™ƒ")
    }

    base_alert_payload(type, alert)
  end

  defp pronoun_to("F"), do: dgettext("apns", "her TO")
  defp pronoun_to("M"), do: dgettext("apns", "him TO")
  defp pronoun_to(_), do: dgettext("apns", "them TO")

  defp pronoun_belonging_to("F"), do: dgettext("apns", "her BELONGING TO")
  defp pronoun_belonging_to("M"), do: dgettext("apns", "his BELONGING TO")
  defp pronoun_belonging_to(_), do: dgettext("apns", "their BELONGING TO")
end
